## Small function to extract numbers from rarefaction values
import re
def rafe_value_fct(wildcards):
    if wildcards == "norarefaction":
        value = "norarefaction"
    else :
        value = re.findall('\d+',wildcards)
    return value


rule to_Phyloseq_and_rarefy:
    conda:
        "../../envs/r_visualization.yml"
    input:
        count_table = "{tool}/2_denoised/count_table.txt",
        Metadata_table = config["local_samples"],
        taxonomy_table = "{tool}/3_classified/{classifier}/{db_taxonomy}/dna-sequences_tax_assignments.txt",
        rep_seqs = "{tool}/2_denoised/dna-sequences.fasta"
    output:
        phyloseq_object = "{tool}/5_visualization/{classifier}/{db_taxonomy}/{rarefaction_value}/physeq/no_collapse/base.rds"
    params:
        rarefaction_value = lambda wildcards: rafe_value_fct(wildcards.rarefaction_value),
        viz_replace_empty_tax = config["viz_replace_empty_tax"]
    log:
        logging_folder + "{tool}/5_visualization/{classifier}/{db_taxonomy}/{rarefaction_value}/physeq/no_collapse/base_physeq.txt"
    script:
        "scripts/physeq_rarefy_and_import.R"


rule rarefaction_curve:
    conda:
        "../../envs/r_visualization.yml"
    input:
        phyloseq_object = "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/no_collapse/base.rds",
        Metadata_table = config["local_samples"]
    output:
        rarefaction_curve = "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/rarefaction_curve.png"
    params:
        sample_type = config["sample_type"],
        sample_label = config["sample_label"]
    log:
        logging_folder +"{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/rarefaction_curve.txt"
    script:
        "scripts/rarefaction_curve.R"


rule Phyloseq_collapse_taxa:
    conda:
        "../../envs/r_visualization.yml"
    input:
        "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/no_collapse/base.rds"
    output:
        "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/collap_{collapse_level}/base.rds"
    params:
        collapse_level = lambda wildcards: wildcards.collapse_level,
    log:
        logging_folder + "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/collap_{collapse_level}/collapse_base.txt"
    script:
        "scripts/physeq_collapse_taxa.R"


rule filter_Phyloseq_taxa:
    conda:
        "../../envs/r_visualization.yml"
    input:
        "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/{collapse_or_not}/base.rds"
    output:
        "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/{collapse_or_not}/1_filter_taxa/{filter_tax_rank}_{filter_lineage}_taxfilt.rds"
    params:
        filter_tax_rank = lambda wildcards: wildcards.filter_tax_rank,
        filter_lineage = lambda wildcards: wildcards.filter_lineage,
        filter_out_tax_rank = config["filter_out_tax_rank"],
        filter_out_lineage = config["filter_out_lineage"],
        collapse_level = lambda wildcards: wildcards.collapse_or_not
    log:
        logging_folder + "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/{collapse_or_not}/1_filter_taxa/{filter_tax_rank}_{filter_lineage}_taxfilt.txt"
    script:
        "scripts/physeq_filter_taxa.R"


rule filter_Phyloseq_samples:
    conda:
        "../../envs/r_visualization.yml"
    input:
         "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/{collapse_or_not}/1_filter_taxa/{prefix}_taxfilt.rds"
    output:
        "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/{collapse_or_not}/2_filter_samples/{prefix}_taxfilt_{filter_column_value}_in_{filter_meta_column}_samples.rds"
    params:
        filter_meta_column = lambda wildcards: wildcards.filter_meta_column,
        filter_column_value = lambda wildcards: wildcards.filter_column_value
    log:
        logging_folder + "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/{collapse_or_not}/2_filter_samples/{prefix}_{filter_column_value}_in_{filter_meta_column}_samples.txt"
    script:
        "scripts/physeq_filter_samples.R"


rule filter_Phyloseq_features:
    conda:
        "../../envs/r_visualization.yml"
    input:
        "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/{collapse_or_not}/2_filter_samples/{prefix}_samples.rds"
    output:
        "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/{collapse_or_not}/3_filter_features/{prefix}_features.rds"
    params:
        filter_features_subset_relative_or_absolute = config["filter_features_subset_relative_or_absolute"],
        filter_features_subset_formula = config["filter_features_subset_formula"],
        collapse_level = lambda wildcards: wildcards.collapse_or_not
    log:
        logging_folder + "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/{collapse_or_not}/3_filter_features/{prefix}_features.txt"
    script:
        "scripts/physeq_subset_features.R"


rule Phyloseq_transform_to_percent:
    conda:
        "../../envs/r_visualization.yml"
    input:
        "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/{collapsed_or_not}/{prefix1}/{prefix2}.rds"
    output:
        "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/{collapsed_or_not}/{prefix1}/{prefix2}_trfs.rds",
    log:
        logging_folder + "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/{collapsed_or_not}/{prefix1}/{prefix2}_trfs."
    script:
        "scripts/physeq_pct_trsf.R"


rule melt_Phyloseq_object:
    conda:
        "../../envs/r_visualization.yml"
    input:
        "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/{collapsed_or_not}/{prefix1}.rds"
    output:
        "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/{collapsed_or_not}/{prefix1}_melted.tsv"
    log:
        logging_folder + "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/{collapsed_or_not}/{prefix1}_melted.txt"
    script:
        "scripts/physeq_melt_table.R"


rule export_Phyloseq_object:
    conda:
        "../../envs/r_visualization.yml"
    input:
        "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/{collapsed_or_not}/{prefix1}.rds"
    output:
        tree_path = "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/{collapsed_or_not}/{prefix1}_export/tree.tree",
        meta_path = "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/{collapsed_or_not}/{prefix1}_export/metadata_table.tsv",
        taxonomy_path = "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/{collapsed_or_not}/{prefix1}_export/dna-sequences_tax_assignments.txt",
        OTU_path = "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/{collapsed_or_not}/{prefix1}_export/count_table.txt",
        filtered_rep_seq = "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/{collapsed_or_not}/{prefix1}_export/filt_rep_seqs.fasta",
    log:
        logging_folder + "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/{collapsed_or_not}/{prefix1}_export/export.txt",
    script:
        "scripts/physeq_export.R"


rule transpose_and_meta_count_table:
    conda:
        "../../envs/r_visualization.yml"
    input:
        count_table = "{prefix}/count_table.txt",
        meta = "{prefix}/metadata_table.tsv"
    output:
        transposed_table = "{prefix}/count_table_transposed.txt",
        merged_meta = "{prefix}/count_table_transposed_with_meta.txt"
    priority:
        1
    log:
        logging_folder + "{prefix}/count_table_transposed.log"
    script:
        "scripts/transpose_and_add_meta_count_table.R"
