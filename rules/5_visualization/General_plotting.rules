
## Plotting data

### Build stat table for raw reads and processed reads
rule raw_to_processed_reads_stats:
    conda:
        "../../envs/r_visualization.yml"
    input:
        phyloseq_object = "{denoiser}/4_physeq/{classifier}_{tax_DB}/nofiltering/norarefaction/no_collapse/base.rds",
        multi_QC_report_path = "QC/multiqc_raw_reads_report_data/multiqc_general_stats.txt"
    output:
        raw_to_filtered_reads_stats = "{denoiser}/5_visualization/{classifier}_{tax_DB}/reads/raw_to_filtered_reads_stats.tsv"
    log:
        logging_folder + "{denoiser}/5_visualization/{classifier}_{tax_DB}/reads/raw_to_filtered_reads_stats.txt"
    script:
        "scripts/raw_to_processed_reads_stats.R"

### Plot stat table for raw reads and processed reads
rule raw_to_processed_reads_plot:
    conda:
        "../../envs/r_visualization.yml"
    input:
        raw_to_filtered_reads_stats = "{denoiser}/5_visualization/{classifier}_{tax_DB}/reads/raw_to_filtered_reads_stats.tsv",
        Metadata_table = config["local_samples"],
        multi_QC_report_path = "QC/multiqc_raw_reads_report_data/multiqc_general_stats.txt"
    output:
        reads_plot_with_filtered = "{denoiser}/5_visualization/{classifier}_{tax_DB}/reads/reads_plot_with_filtered.png"
    params:
        grouping_column = config["grouping_column"],
        sample_label = config["sample_label"]
    log:
        logging_folder + "{denoiser}/5_visualization/{classifier}_{tax_DB}/reads/reads_plot_with_filtered.txt"
    script:
        "scripts/raw_to_processed_reads_plot.R"


### Build stat table for raw reads, processed reads and taxonomically filtered reads
rule raw_to_tax_filtered_reads_stats:
    conda:
        "../../envs/r_visualization.yml"
    input:
        read_filtering = "{denoiser}/4_physeq/{classifier}_{tax_DB}/nofiltering/norarefaction/no_collapse/base.rds",
        taxonomic_filtering = "{denoiser}/4_physeq/{classifier}_{tax_DB}/{filter_lineage}_in_{filter_tax_rank}/norarefaction/no_collapse/base.rds",
        multi_QC_report_path = "QC/multiqc_raw_reads_report_data/multiqc_general_stats.txt"
    output:
        raw_to_filtered_reads_stats = "{denoiser}/5_visualization/{classifier}_{tax_DB}/reads/reads_stats_with_{filter_lineage}_in_{filter_tax_rank}_filtered.tsv"
    log:
        logging_folder + "{denoiser}/5_visualization/{classifier}_{tax_DB}/reads/reads_stats_with_{filter_lineage}_in_{filter_tax_rank}_filtered.txt"
    script:
        "scripts/raw_to_tax_filtered_reads_stats.R"


### Plot stat table for raw reads, processed reads and taxonomically filtered reads
rule raw_to_tax_filtered_reads_plots:
    conda:
        "../../envs/r_visualization.yml"
    input:
        raw_to_filtered_reads_stats = "{denoiser}/5_visualization/{classifier}_{tax_DB}/reads/reads_stats_with_{filter_lineage}_in_{filter_tax_rank}_filtered.tsv",
        Metadata_table = config["local_samples"],
        multi_QC_report_path = "QC/multiqc_raw_reads_report_data/multiqc_general_stats.txt"
    output:
        reads_plot_with_filtered = "{denoiser}/5_visualization/{classifier}_{tax_DB}/reads/reads_plot_with_{filter_lineage}_in_{filter_tax_rank}_filtered.png"
    params:
        sample_label = config["sample_label"]
    log:
        logging_folder + "{denoiser}/5_visualization/{classifier}_{tax_DB}/reads/reads_plot_with_{filter_lineage}_in_{filter_tax_rank}_filtered.txt"
    script:
        "scripts/raw_to_processed_reads_plot.R"


### Plot interactive KRONA plots
rule KRONA_plots:
    conda:
        "../../envs/r_visualization.yml"
    input:
        phyloseq_melted_table = "{denoiser}/4_physeq/{classifier}_{tax_DB}/nofiltering/norarefaction/no_collapse/base_melted.tsv"
    output:
        output = "{denoiser}/5_visualization/{classifier}_{tax_DB}/KRONA/{grouping_column}/{grouping_col_value}.html"
    params:
        grouping_column = lambda wildcards: wildcards.grouping_column,
        grouping_col_value = lambda wildcards: wildcards.grouping_col_value,
        sample_label = config["sample_label"]
    log:
        logging_folder + "{denoiser}/5_visualization/{classifier}_{tax_DB}/KRONA/{grouping_column}/{grouping_col_value}.txt"
    script:
        "scripts/KRONA_plots.R"

### Generate rarafaction curves to observe the effect of coverage on alpha-diversity metrics
rule rarefaction_curve:
    conda:
        "../../envs/r_visualization.yml"
    input:
        phyloseq_object = "{denoiser}/4_physeq/{classifier}_{tax_DB}/{filtration_or_not}/{raref_or_not}/no_collapse/base.rds",
        Metadata_table = config["local_samples"]
    output:
        rarefaction_curve = "{denoiser}/5_visualization/{classifier}_{tax_DB}/rarefaction_curve/{filtration_or_not}/{raref_or_not}_rarefaction_curve.png"
    params:
        color_factor = config["color_factor"],
        sample_label = config["sample_label"]
    log:
        logging_folder + "{denoiser}/5_visualization/{classifier}_{tax_DB}/rarefaction_curve/{filtration_or_not}/{raref_or_not}_rarefaction_curve.txt"
    script:
        "scripts/rarefaction_curve.R"


### A small function to transform the taxa levels to the collapse levels, for barplots and heatmaps
def barplot_levels(wildcards):
    # print(wildcards)

    if wildcards == "OTU":
        value = 'no_collapse'
    elif wildcards == "Species" :
        value = 'collap_7'
    elif wildcards == "Genus" :
        value = 'collap_6'
    elif wildcards == "Family" :
        value = 'collap_5'
    elif wildcards == "Order" :
        value = 'collap_4'
    elif wildcards == "Class" :
        value = 'collap_3'
    elif wildcards == "Phylum" :
        value = 'collap_2'
    elif wildcards == "Kingdom" :
        value = 'collap_1'
    else :
        raise ValueError("Forbidden value in taxa level for barplots")

    # print(value)

    return(value)

### Plot taxonomic barplots
rule barplots:
    conda:
        "../../envs/barplots.yml"
    input:
        phyloseq_melted_table = lambda wildcards: "{denoiser}/4_physeq/{classifier}_{tax_DB}/{filter_lineage}_in_{filter_tax_rank}/norarefaction/" + barplot_levels(wildcards.tax_ranks) + "/base_melted.tsv"
    output:
        barplot = "{denoiser}/5_visualization/{classifier}_{tax_DB}/barplots/{filter_lineage}_in_{filter_tax_rank}/{relative_or_absolute_plot}/{grouping_column}_{relative_or_absolute_filtering}_{filtering_value}_{tax_ranks}_barplot.pdf",
    params:
        sample_label = config["sample_label"],
        grouping_column = lambda wildcards: wildcards.grouping_column,
        t_neg_PCR_sample_on_plots = config["t_neg_PCR_sample_on_plots"],
        t_neg_PCR_group_column_value = config["t_neg_PCR_group_column_value"],
        relative_or_absolute_filtering = lambda wildcards: wildcards.relative_or_absolute_filtering,
        filtering_value = lambda wildcards: wildcards.filtering_value,
        relative_or_absolute_plot = lambda wildcards: wildcards.relative_or_absolute_plot,
        tax_ranks = lambda wildcards: wildcards.tax_ranks,
        distinct_colors = config["distinct_colors"],
        horizontal_plot = config["horizontal_plot"],
        facet_plot = config["facet_plot"],
        facetting_column = config["facetting_column"],
        order_by_abundance = config["order_by_abundance"],
        separated_legend = config["separated_legend"],
        abundance_filtre_level = config["abundance_filtre_level"]
    log:
        logging_folder + "{denoiser}/5_visualization/{classifier}_{tax_DB}/barplots/{filter_lineage}_in_{filter_tax_rank}/{relative_or_absolute_plot}/{grouping_column}_{relative_or_absolute_filtering}_{filtering_value}_{tax_ranks}_barplot.txt",
    script:
        "scripts/barplot.R"


### Plot taxonomic heatmaps
rule heatmaps:
    conda:
        "../../envs/r_visualization.yml"
    input:
        phyloseq_melted_table = lambda wildcards: "{denoiser}/4_physeq/{classifier}_{tax_DB}/{filter_lineage}_in_{filter_tax_rank}/norarefaction/" + barplot_levels(wildcards.plotting_tax_ranks) + "/base_melted.tsv"
    output:
        heatmap = "{denoiser}/5_visualization/{classifier}_{tax_DB}/heatmaps/{filter_lineage}_in_{filter_tax_rank}/{relative_or_absolute_plot}/{grouping_column}_{relative_or_absolute_filtering}_{filtering_value}_{plotting_tax_ranks}_heatmap.pdf",
    params:
        sample_label = config["sample_label"],
        grouping_column = lambda wildcards: wildcards.grouping_column,
        t_neg_PCR_sample_on_plots = config["t_neg_PCR_sample_on_plots"],
        t_neg_PCR_group_column_value = config["t_neg_PCR_group_column_value"],
        relative_or_absolute_filtering = lambda wildcards: wildcards.relative_or_absolute_filtering,
        filtering_value = lambda wildcards: wildcards.filtering_value,
        relative_or_absolute_plot = lambda wildcards: wildcards.relative_or_absolute_plot,
        plotting_tax_ranks = lambda wildcards: wildcards.plotting_tax_ranks,
        horizontal_plot = config["horizontal_plot"],
        facet_plot = config["facet_plot"],
        facetting_column = config["facetting_column"],
        order_by_abundance = config["order_by_abundance"],
        abundance_filtre_level = config["abundance_filtre_level"]
    log:
        logging_folder + "{denoiser}/5_visualization/{classifier}_{tax_DB}/heatmaps/{filter_lineage}_in_{filter_tax_rank}/{relative_or_absolute_plot}/{grouping_column}_{relative_or_absolute_filtering}_{filtering_value}_{plotting_tax_ranks}_heatmap.txt",
    script:
        "scripts/heatmap2.R"

### Plot set of alpha-diversity metrics
rule alpha_diversity:
    conda:
        "../../envs/r_visualization.yml"
    input:
        metadata = "{denoiser}/4_physeq/{classifier}_{tax_DB}/{filtered_or_not}/{raref_or_not}/no_collapse/base_with_tree_trfs_export/metadata_table.tsv",
    output:
        alpha_plot = "{denoiser}/5_visualization/{classifier}_{tax_DB}/{raref_or_not}/alpha_diversities/{filtered_or_not}/{grouping_column}_{alpha_diversity}.pdf",
    params:
        grouping_column = lambda wildcards: wildcards.grouping_column,
        alpha_metric = lambda wildcards: wildcards.alpha_diversity,
        sample_label = config["sample_label"],
        color_factor = config["color_factor"],
        facet_plot = config["facet_plot"],
        facetting_column = config["facetting_column"],
    log:
        logging_folder + "{denoiser}/5_visualization/{classifier}_{tax_DB}/{raref_or_not}/alpha_diversities/{filtered_or_not}/{grouping_column}_{alpha_diversity}.txt"
    script:
        "scripts/alpha_diversity.R"


### Plot PCA based on beta-diversity distances
rule ordination_distance_based:
    conda:
        "../../envs/r_visualization.yml"
    input:
        phyloseq_object = "{denoiser}/4_physeq/{classifier}_{tax_DB}/{filter_lineage}_in_{filter_tax_rank}/{raref_or_not}/no_collapse/base_with_tree.rds",
        Metadata_table = config["local_samples"]
    output:
        ordination = "{denoiser}/5_visualization/{classifier}_{tax_DB}/{raref_or_not}/ordination_distance_based/{filter_lineage}_in_{filter_tax_rank}/{ordination_method}/{grouping_column}_{ordination_distance}_d_{shape_factor}.pdf",
    params:
        grouping_column = lambda wildcards: wildcards.grouping_column,
        color_factor = config["color_factor"],
        ordination_distance = lambda wildcards: wildcards.ordination_distance ,
        ordination_method = lambda wildcards: wildcards.ordination_method,
        shape_factor = lambda wildcards: config["shape_factor"]
    log:
        logging_folder + "{denoiser}/5_visualization/{classifier}_{tax_DB}/{raref_or_not}/ordination_distance_based/{filter_lineage}_in_{filter_tax_rank}/{ordination_method}/{grouping_column}_{ordination_distance}_d_{shape_factor}.txt",
    script:
        "scripts/ordination_distance_based.R"


### Plot unconstrained PCA
rule ordination_unconstrained:
    conda:
        "../../envs/r_visualization.yml"
    input:
        phyloseq_object = "{denoiser}/4_physeq/{classifier}_{tax_DB}/{filter_lineage}_in_{filter_tax_rank}/{raref_or_not}/no_collapse/base_with_tree.rds",
        Metadata_table = config["local_samples"]
    output:
        unconstrained_ordination = "{denoiser}/5_visualization/{classifier}_{tax_DB}/{raref_or_not}/ordination_unconstrained/{filter_lineage}_in_{filter_tax_rank}/{ordination_method}/{grouping_column}_u_{shape_factor}.pdf",
    params:
        grouping_column = lambda wildcards: wildcards.grouping_column,
        color_factor = config["color_factor"],
        ordination_method = lambda wildcards: wildcards.ordination_method,
        shape_factor = lambda wildcards: config["shape_factor"]
    log:
        logging_folder + "{denoiser}/5_visualization/{classifier}_{tax_DB}/{raref_or_not}/ordination_unconstrained/{filter_lineage}_in_{filter_tax_rank}/{ordination_method}/{grouping_column}_u_{shape_factor}.txt",
    script:
        "scripts/ordination_unconstrained.R"


### Plot constrained PCA
rule ordination_constrained:
    conda:
        "../../envs/r_visualization.yml"
    input:
        phyloseq_object = "{denoiser}/4_physeq/{classifier}_{tax_DB}/{filter_lineage}_in_{filter_tax_rank}/{raref_or_not}/no_collapse/base_with_tree.rds",
        Metadata_table = config["local_samples"]
    output:
        constrained_ordination = "{denoiser}/5_visualization/{classifier}_{tax_DB}/{raref_or_not}/ordination_constrained/{filter_lineage}_in_{filter_tax_rank}/{ordination_method}/{grouping_column}_c_{shape_factor}.pdf",
    params:
        grouping_column = lambda wildcards: wildcards.grouping_column,
        color_factor = config["color_factor"],
        ordination_method = lambda wildcards: wildcards.ordination_method,
        shape_factor = lambda wildcards: wildcards.shape_factor
    log:
        logging_folder + "{denoiser}/5_visualization/{classifier}_{tax_DB}/{raref_or_not}/ordination_constrained/{filter_lineage}_in_{filter_tax_rank}/{ordination_method}/{grouping_column}_c_{shape_factor}.txt"
    script:
        "scripts/ordination_constrained.R"

