

import pandas as pd
import re


rule decompress_assemblies:
    input:
        'assembly_gz/{sample}.fna.gz'
    output:
        temp('assembly_gz/{sample}.fna')
    shell:
        "zcat {input[0]} > {output[0]}"


rule print_primers_to_files :
    output:
        "InSilico/PCR/Primers.fasta"
    params:
        forward = config["forward_primer"],
        reverse = config["reverse_primer"]
    shell:
        '''
        printf \
        ">primer|F \n{params[0]} \n>primer|R \n{params[1]}" >> {output}
        '''



rule Extract_V3V4 :
    conda :
        "../../envs/simulate_PCR.yml"
    input :
        "InSilico/PCR/Primers.fasta",
        "assembly_gz/{sample}.fna"
    output :
        "InSilico/PCR/{sample}/Primers.fasta.pair.{sample}.fna.amplicons.fasta",
    params:
        min_length = config["merged_min_length"],
        max_length = config["merged_max_length"],

    threads :
        1
    shell :
        '''
        cp {input[1]} $(dirname {output[0]}) && \
        cd $(dirname {output[0]}) && \
        simulate_PCR \
            -primers ../../../{input[0]} \
            -db $(basename {input[1]}) \
            -minlen 390 \
            -maxlen 600 \
            -mm 3 \
            -mux 0 \
            -num_threads {threads} \
            -max_target_seqs 1000000000 \
            -word_size 4 \
            -evalue 100000000 \
            -genes 1 \
            -extract_amp 1 \
            -3prime 2
         '''


def list_amplicons(wildcards):
    checkpoint_output = checkpoints.combine_assembly_tables.get(**wildcards).output[0]
    table=pd.read_csv(checkpoint_output,delimiter='\t')
    acc=list(table['AssemblyNames'])
    expd=expand("InSilico/PCR/{sample}/Primers.fasta.pair.{sample}.fna.amplicons.fasta",sample=acc)
    return expd

#primer enlever


### Combine all extracted sequences in one big fasta
rule Insilico_merge_all_in_one_fasta:
    input:
        list_amplicons
    output:
        "InSilico/1c_derep/merged_all.fasta"
    shell:
        '''
        cat {input} >> {output}
        '''


### Again, dereplicate all identical sequences after merging. Sequences must at least be twice in dataset to be kept.
rule InSilico_derepicate_all:
    conda:
        "../../envs/QIIME1.yml"
    input:
        "InSilico/1c_derep/merged_all.fasta"
    output:
        "InSilico/2_denoised/dna-sequences.fasta"
    log:
         logging_folder + "InSilico/1c_all_merged_sequences/dereplicate_all.txt"
    shell:
        '''
        vsearch --derep_fulllength {input} \
                --sizeout \
                --minuniquesize 2 \
                --output {output} \
                2> {log}
        '''


def list_samples(wildcards):
    checkpoint_output = checkpoints.combine_assembly_tables.get(**wildcards).output[0]
    table=pd.read_csv(checkpoint_output,delimiter='\t')
    acc=list(table['AssemblyNames'])
    expd=expand('assembly_gz/{sample}.fna',sample=acc)
    return expd



### Count the number of occurences of the representative sequences in the samples.
rule InSilico_count_occurences :
    conda:
        "../../envs/QIIME1.yml"
    input:
        samples = "InSilico/PCR/{sample}/Primers.fasta.pair.{sample}.fna.amplicons.fasta",
        rep_seq = "InSilico/2_denoised/dna-sequences.fasta"
    output:
        "InSilico/2_denoised/countOTUs/{sample}_count_table.txt",
    log:
         logging_folder + "InSilico/2_denoised/countOTUs/{sample}_count_table.txt"
    shell:
        '''
        vsearch --usearch_global {input[samples]} \
        -otutabout {output} \
        -id 1 \
        -strand plus \
        --db {input[rep_seq]} \
        2> {log}
        '''


def list_samples_counts(wildcards):
    checkpoint_output = checkpoints.combine_assembly_tables.get(**wildcards).output[0]
    table=pd.read_csv(checkpoint_output,delimiter='\t')
    acc=list(table['AssemblyNames'])
    expd=expand("InSilico/2_denoised/countOTUs/{sample}_count_table.txt",sample=acc)
    return expd


### Format count table from InSilico
rule create_InSilico_count_table :
    conda:
        "../../envs/r_visualization.yml"
    input:
        list_samples_counts
    output:
        count_table = "InSilico/2_denoised/count_table.txt"
    log:
        logging_folder +  "InSilico/2_denoised/count_table.txt"
    script:
        "scripts/create_count_table_from_insilico.R"












