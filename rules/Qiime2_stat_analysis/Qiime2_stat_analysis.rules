
rule Qiime2_samples_filtration :
    conda:
        "../../envs/QIIME2-2018.8.yml"
    input:
        count_table = "{tool}/2_denoised/count-table.qza",
        Metadata_table = config["local_samples"],
    output:
        output1 = "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/differential_abundance/filtered_samples.qza",
    params:
        filtration = '''%s="%s"''' % (config["filtration_column"], config["filtration_value"])
    log:
        logging_folder + "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/differential_abundance/filtered_samples.txt"
    shell:
        '''
        qiime feature-table filter-samples \
            --i-table {input[count_table]} \
            --m-metadata-file {input[Metadata_table]} \
            --p-where '"{config[filtration_column]}"="{config[filtration_value]}"' \
            --o-filtered-table {output[output1]}
        '''

rule create_visualize_table_QIIME2_filtered :
    conda:
        "../../envs/QIIME2-2018.8.yml"
    input:
        table = "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/differential_abundance/filtered_samples.qza",
        Metadata_table = config["local_samples"],
    output:
        "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/differential_abundance/filtered_samples.qzv"
    log:
        logging_folder + "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/differential_abundance/filtered_samples_qzv.txt"
    shell:
        '''
        qiime feature-table summarize \
            --i-table {input[table]} \
            --o-visualization {output} \
            --m-sample-metadata-file {input[Metadata_table]}
        '''


rule Qiime2_pseudocounts_no_collapse :
    conda:
        "../../envs/QIIME2-2018.8.yml"
    input:
        "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/differential_abundance/filtered_samples.qza"
    output:
        "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/differential_abundance/no_collapse/comp_filtered_samples.qza"
    log:
        logging_folder + "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/differential_abundance/no_collapse/comp_filtered_samples.txt"
    shell:
        '''
        qiime composition add-pseudocount \
        --i-table {input} \
        --o-composition-table {output}
        '''


rule Qiime2_ANCOM_no_collapse :
    conda:
        "../../envs/QIIME2-2018.8.yml"
    input:
        table = "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/differential_abundance/no_collapse/comp_filtered_samples.qza",
        Metadata = config["local_samples"],
    output:
        "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/differential_abundance/no_collapse/ANCOM/ANCOM_{tested_factor}.qzv"
    params:
        tested_factor = lambda wildcards: wildcards.tested_factor
    log:
        logging_folder + "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/differential_abundance/no_collapse/ANCOM/ANCOM_{tested_factor}.txt"
    shell:
        '''
        qiime composition ancom \
          --i-table {input[table]} \
          --m-metadata-file {input[Metadata]} \
          --m-metadata-column {params[tested_factor]} \
          --o-visualization {output}
        '''


rule Qiime2_taxa_collapse :
    conda:
        "../../envs/QIIME2-2018.8.yml"
    input:
        table = "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/differential_abundance/filtered_samples.qza",
        taxonomy = "{tool}/3_classified/{classifier}/{db_taxonomy}/dna-sequences_tax_assignments.qza"
    output:
         output1 = "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/differential_abundance/taxa_collapse_{collapse_level}/coll_filtered_samples.qza"
    params:
        collapse_level = lambda wildcards: wildcards.collapse_level
    log:
        logging_folder + "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/differential_abundance/taxa_collapse_{collapse_level}/coll_filtered_samples.txt"
    shell:
        '''
        qiime taxa collapse \
          --i-table {input[table]} \
          --i-taxonomy {input[taxonomy]} \
          --p-level {wildcards.collapse_level} \
          --o-collapsed-table {output}
        '''


rule Qiime2_pseudocounts_collapsed :
    conda:
        "../../envs/QIIME2-2018.8.yml"
    input:
        "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/differential_abundance/taxa_collapse_{collapse_level}/coll_filtered_samples.qza"
    output:
        "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/differential_abundance/taxa_collapse_{collapse_level}/comp_filtered_samples.qza"
    log:
        logging_folder + "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/differential_abundance/taxa_collapse_{collapse_level}/comp_filtered_samples.txt"
    shell:
        '''
        qiime composition add-pseudocount \
        --i-table {input} \
        --o-composition-table {output}
        '''

rule Qiime2_ANCOM_collapsed :
    conda:
        "../../envs/QIIME2-2018.8.yml"
    input:
        table = "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/differential_abundance/taxa_collapse_{collapse_level}/comp_filtered_samples.qza",
        Metadata = config["local_samples"],
    output:
        "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/differential_abundance/taxa_collapse_{collapse_level}/ANCOM/ANCOM_{tested_factor}.qzv"
    params:
        tested_factor = lambda wildcards: wildcards.tested_factor
    log:
        logging_folder + "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/differential_abundance/taxa_collapse_{collapse_level}/ANCOM/ANCOM_{tested_factor}.txt"
    shell:
        '''
        qiime composition ancom \
          --i-table {input[table]} \
          --m-metadata-file {input[Metadata]} \
          --m-metadata-column {params[tested_factor]} \
          --o-visualization {output}
        '''

rule Qiime2_Gneiss_gradient_clustering :
    conda:
        "../../envs/QIIME2-2018.8.yml"
    input:
        table = "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/filtered_samples.qza",
        Metadata = config["local_samples"],
    output:
        "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/differential_abudance/no_collapse/Gneiss_{tested_factor}.qza"
    params:
        tested_factor = lambda wildcards: wildcards.tested_factor
    log:
        logging_folder + "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/differential_abudance/no_collapse/Gneiss_{tested_factor}.txt"
    shell:
        '''
        qiime gneiss gradient-clustering \
          --i-table {input[table]} \
          --m-gradient-file {input[Metadata]}  \
          --m-gradient-column {params[tested_factor]} \
          --o-clustering {output}
        '''

rule Qiime2_Gneiss_correlation_clustering :
    conda:
        "../../envs/QIIME2-2018.8.yml"
    input:
        table = "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/filtered_samples.qza",
    output:
        "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/differential_abudance/no_collapse/Gneiss_correlation.qza"
    log:
        logging_folder + "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/differential_abudance/no_collapse/Gneiss_correlation.txt"
    shell:
        '''
        qiime gneiss correlation-clustering \
          --i-table {input[table]} \
          --o-clustering {output}
        '''

rule Qiime2_Gneiss_ilr_hierarchical :
    conda:
        "../../envs/QIIME2-2018.8.yml"
    input:
        table = "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/filtered_samples.qza",
        tree = "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/differential_abudance/no_collapse/Gneiss_{Gneiss_parameter}.qza"
    output:
        "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/differential_abudance/no_collapse/Gneiss_{Gneiss_parameter}_balances.qza"
    log:
        logging_folder + "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/differential_abudance/no_collapse/Gneiss_{Gneiss_parameter}_balances.txt"
    shell:
        '''
        qiime gneiss ilr-hierarchical \
          --i-table {input[table]} \
          --i-tree {input[tree]} \
          --o-balances {output}
        '''

rule Qiime2_Gneiss_regression :
    conda:
        "../../envs/QIIME2-2018.8.yml"
    input:
        table = "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/differential_abudance/no_collapse/Gneiss_{Gneiss_parameter}_balances.qza",
        Metadata = config["local_samples"],
        tree = "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/differential_abudance/no_collapse/Gneiss_{Gneiss_parameter}.qza"
    output:
        "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/differential_abudance/no_collapse/Gneiss_{Gneiss_parameter}_clustering.qzv"
    log:
        logging_folder + "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/differential_abudance/no_collapse/Gneiss_{Gneiss_parameter}_clustering.txt"
    shell:
        '''
        qiime gneiss ols-regression \
          --p-formula "{config[filtration_column]}" \
          --i-table {input[table]} \
          --i-tree {input[tree]} \
          --m-metadata-file {input[Metadata]} \
          --o-visualization {output}
        '''
