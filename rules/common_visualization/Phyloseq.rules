
rule to_Phyloseq:
    conda:
        "../../envs/R_visualization.yml"
    input:
        count_table = "{tool}/2_denoised/count_table.txt",
        Metadata_table = config["local_samples"],
        taxonomy_table = "{tool}/3_classified/{classifier}/{db_taxonomy}/dna-sequences_tax_assignments.txt",
        tax_tree = "{tool}/4_tree/tree.nwk"
    output:
        phyloseq_object = "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/base.rds"
    log:
        logging_folder + "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/base_physeq.txt"
    script:
        "scripts/physeq_import.R"


rule filter_Phyloseq_taxa:
    conda:
        "../../envs/R_visualization.yml"
    input:
        "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/base.rds"
    output:
        "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/{filter_tax_rank}_{filter_lineage}_taxfilt.rds"
    params:
        filter_tax_rank = lambda wildcards: wildcards.filter_tax_rank,
        filter_lineage = lambda wildcards: wildcards.filter_lineage
    log:
        logging_folder + "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/{filter_tax_rank}_{filter_lineage}_taxfilt.txt"
    script:
        "scripts/physeq_filter_taxa.R"


rule filter_Phyloseq_samples:
    conda:
        "../../envs/R_visualization.yml"
    input:
        "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/{filter_tax_rank}_{filter_lineage}_taxfilt.rds"
    output:
        "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/{filter_tax_rank}_{filter_lineage}_taxfilt_{column_value}_in_{meta_column}_samples.rds"
    params:
        meta_column = lambda wildcards: wildcards.meta_column,
        column_value = lambda wildcards: wildcards.column_value
    log:
        logging_folder + "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/{filter_tax_rank}_{filter_lineage}_taxfilt_{column_value}_in_{meta_column}.txt"
    script:
        "scripts/physeq_filter_samples.R"


rule filter_Phyloseq_features:
    conda:
        "../../envs/R_visualization.yml"
    input:
        "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/{filter_tax_rank}_{filter_lineage}_taxfilt_{column_value}_in_{meta_column}_samples.rds"
    output:
        "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/{filter_tax_rank}_{filter_lineage}_taxfilt_{column_value}_in_{meta_column}_featuresfilt.rds"
    params:
        subset_formula = config["subset_formula"]
    log:
        logging_folder + "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/{filter_tax_rank}_{filter_lineage}_taxfilt_{column_value}_in_{meta_column}_featuresfilt.txt"
    script:
        "scripts/physeq_subset_features.R"


rule Phyloseq_collapse_taxa:
    conda:
        "../../envs/R_visualization.yml"
    input:
        "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/{filter_tax_rank}_{filter_lineage}_taxfilt_{column_value}_in_{meta_column}_featuresfilt.rds"
    output:
        "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/{filter_tax_rank}_{filter_lineage}_taxfilt_{column_value}_in_{meta_column}_featuresfilt_{collapse_level}_collapsed.rds"
    params:
        collapse_level = lambda wildcards: wildcards.collapse_level,
    log:
        logging_folder + "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/{filter_tax_rank}_{filter_lineage}_taxfilt_{column_value}_in_{meta_column}_featuresfilt_{collapse_level}_collapsed.txt"
    script:
        "scripts/physeq_collapse_taxa.R"


rule melt_Phyloseq_object:
    conda:
        "../../envs/R_visualization.yml"
    input:
        "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/{prefix}.rds"
    output:
        "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/{prefix}_melted.tsv"
    log:
        logging_folder + "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/phyloseq/{prefix}_melted.txt"
    script:
        "scripts/physeq_melt_table.R"

