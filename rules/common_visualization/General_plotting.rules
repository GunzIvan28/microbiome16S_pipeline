
rule raw_to_filtered_reads_stats:
    conda:
        "../../envs/r_visualization.yml"
    input:
        phyloseq_object = "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/no_collapse/base.rds",
        multi_QC_report_path = "QC/multiqc_raw_reads_report_data/multiqc_general_stats.txt"
    output:
        raw_to_filtered_reads_stats = "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/reads/raw_to_filtered_reads_stats.tsv"
    log:
        logging_folder + "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/reads/raw_to_filtered_reads_stats.txt"
    script:
        "scripts/raw_to_filtered_reads_stats.R"


rule reads_plot_with_filtered:
    conda:
        "../../envs/r_visualization.yml"
    input:
        raw_to_filtered_reads_stats_path = "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/reads/raw_to_filtered_reads_stats.tsv",
        Metadata_table = config["local_samples"],
        multi_QC_report_path = "QC/multiqc_raw_reads_report_data/multiqc_general_stats.txt"
    output:
        reads_plot_with_filtered = "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/reads/reads_plot_with_filtered.png"
    params:
        grouping_column = config["grouping_column"],
        x_axis_column = config["x_axis_column"]
    log:
        logging_folder + "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/reads/reads_plot_with_filtered.txt"
    script:
        "scripts/reads_plots_with_filtration.R"


rule KRONA_plots:
    conda:
        "../../envs/r_visualization.yml"
    input:
        phyloseq_melted_table = "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/no_collapse/base_melted.tsv"
    output:
        output = "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/KRONA/{grouping_column}/{grouping_col_value}.html"
    params:
        grouping_column = lambda wildcards: wildcards.grouping_column,
        grouping_col_value = lambda wildcards: wildcards.grouping_col_value,
        x_axis_column = config["x_axis_column"]
    log:
        logging_folder + "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/{grouping_column}/{grouping_col_value}_KRONA.txt"
    script:
        "scripts/KRONA_plots.R"


rule alpha_diversity:
    conda:
        "../../envs/r_visualization.yml"
    input:
        phyloseq_object = "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/no_collapse/2_filter_samples/{filter_rules}_samples.rds",
        Metadata_table = config["local_samples"]
    output:
        alpha_plot = "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/alpha_diversity/{filter_rules}/{grouping_column}/{grouping_col_value}_alpha_divesity.png",
    params:
        grouping_column = lambda wildcards: wildcards.grouping_column,
        grouping_col_value = lambda wildcards: wildcards.grouping_col_value,
        x_axis_column = config["x_axis_column"],
        sample_type = config["sample_type"]
    log:
        logging_folder + "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/alpha_diversity/{filter_rules}/{grouping_column}/{grouping_col_value}_alpha_divesity.txt"
    script:
        "scripts/alpha_diversity.R"


rule ordination_distance_based:
    conda:
        "../../envs/r_visualization.yml"
    input:
        phyloseq_object = "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/no_collapse/2_filter_samples/{filter_rules}_samples.rds",
        Metadata_table = config["local_samples"]
    output:
        output1 = "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/ordination/{filter_rules}/distance_based/{ordination_method_distance_based}/{grouping_column}/{grouping_col_value}_d_{ordination_distance}.png",
    params:
        grouping_column = lambda wildcards: wildcards.grouping_column,
        grouping_col_value = lambda wildcards: wildcards.grouping_col_value,
        x_axis_column = config["x_axis_column"],
        sample_type = config["sample_type"],
        ordination_distance = lambda wildcards: wildcards.ordination_distance ,
        ordination_method = lambda wildcards: wildcards.ordination_method_distance_based,
        ordination_factor = lambda wildcards: config["ordination_factor"]
    log:
        logging_folder + "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/ordination/{filter_rules}/distance_based/{ordination_method_distance_based}/{grouping_column}/{grouping_col_value}_d_{ordination_distance}.logs",
    script:
        "scripts/ordination_distance_based.R"


rule ordination_unconstrained:
    conda:
        "../../envs/r_visualization.yml"
    input:
        phyloseq_object = "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/no_collapse/2_filter_samples/{filter_rules}_samples.rds",
        Metadata_table = config["local_samples"]
    output:
        output1 = "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/ordination/{filter_rules}/unconstrained/{ordination_method}/{grouping_column}/{grouping_col_value}.png",
    params:
        grouping_column = lambda wildcards: wildcards.grouping_column,
        grouping_col_value = lambda wildcards: wildcards.grouping_col_value,
        x_axis_column = config["x_axis_column"],
        sample_type = config["sample_type"],
        ordination_method = lambda wildcards: wildcards.ordination_method,
        ordination_factor = lambda wildcards: config["ordination_factor"]
    log:
        logging_folder + "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/ordination/{filter_rules}/unconstrained/{ordination_method}/{grouping_column}/{grouping_col_value}_logs.txt"
    script:
        "scripts/ordination_unconstrained.R"


rule ordination_constrained:
    conda:
        "../../envs/r_visualization.yml"
    input:
        phyloseq_object = "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/no_collapse/2_filter_samples/{filter_rules}_samples.rds",
        Metadata_table = config["local_samples"]
    output:
        output1 = "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/ordination/{filter_rules}/constrained/{ordination_method}/{grouping_column}/{grouping_col_value}_f_{ordination_factor}.png",
    params:
        grouping_column = lambda wildcards: wildcards.grouping_column,
        grouping_col_value = lambda wildcards: wildcards.grouping_col_value,
        x_axis_column = config["x_axis_column"],
        sample_type = config["sample_type"],
        ordination_method = lambda wildcards: wildcards.ordination_method,
        ordination_factor = lambda wildcards: wildcards.ordination_factor
    log:
        logging_folder + "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/ordination/{filter_rules}/constrained/{ordination_method}/{grouping_column}/{grouping_col_value}_f_{ordination_factor}.logs.txt"
    script:
        "scripts/ordination_constrained.R"


# A small function to transform the taxa levels to the collapse levels
def barplot_levels(wildcards):
    # print(wildcards)

    if wildcards == "OTU":
        value = 'no_collapse'
    elif wildcards == "Species" :
        value = 'collap_7'
    elif wildcards == "Genus" :
        value = 'collap_6'
    elif wildcards == "Family" :
        value = 'collap_5'
    elif wildcards == "Order" :
        value = 'collap_4'
    elif wildcards == "Class" :
        value = 'collap_4'
    elif wildcards == "Phylum" :
        value = 'collap_3'
    elif wildcards == "Kingdom" :
        value = 'collap_2'
    else :
        raise ValueError("Forbidden value in taxa level for barplots")

    # print(value)

    return(value)


rule barplot:
    singularity:
        "docker://valscherz/16srna:0.0.3-beta"
    input:
        phyloseq_melted_table = lambda wildcards: "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/physeq/" + barplot_levels(wildcards.plotting_tax_ranks) + "/2_filter_samples/{prefix1}_samples_melted.tsv"
    output:
        barplot = "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/barplot/{prefix1}/{relative_or_absolute_plot}/{grouping_column}/{grouping_col_value}_{relative_or_absolute_filtering}_{filtering_value}_{plotting_tax_ranks}_barplot.png",
        barplot_leg = "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/barplot/{prefix1}/{relative_or_absolute_plot}/{grouping_column}/{grouping_col_value}_{relative_or_absolute_filtering}_{filtering_value}_{plotting_tax_ranks}_barplot_leg.png",
    params:
        x_axis_column = config["x_axis_column"],
        grouping_column = lambda wildcards: wildcards.relative_or_absolute_plot,
        t_neg_PCR_sample_on_plots = config["t_neg_PCR_sample_on_plots"],
        t_neg_PCR_group_column_value = config["t_neg_PCR_group_column_value"],
        relative_or_absolute_filtering = lambda wildcards: wildcards.relative_or_absolute_filtering,
        filtering_value = lambda wildcards: wildcards.filtering_value,
        relative_or_absolute_plot = lambda wildcards: wildcards.relative_or_absolute_plot,
        plotting_tax_ranks = lambda wildcards: wildcards.plotting_tax_ranks,
        distinct_colors = config["distinct_colors"],
        horizontal_barplot = config["horizontal_barplot"],
        facet_plot = config["facet_plot"],
        facetting_column = config["facetting_column"],
        order_by_abundance = config["order_by_abundance"],
        separated_legend = config["separated_legend"],
    log:
        logging_folder + "{tool}/5_visualization/{classifier}/{db_taxonomy}/{raref_or_not}/barplot/{prefix1}/{relative_or_absolute_plot}/{grouping_column}/{grouping_col_value}_{relative_or_absolute_filtering}_{filtering_value}_{plotting_tax_ranks}_barplot.txt",
    script:
        "scripts/barplot.R"



