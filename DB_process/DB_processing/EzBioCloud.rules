
rule Import_DB :
    conda :
        "../../envs/QIIME2-2019.01.yml"
    input :
        config["DBpath_seq"]
    output :
        config["Output_folder"] + "EzBioCloud_seq.qza"
    threads :
        1
    shell :
         '''
        qiime tools import \
        --type 'FeatureData[Sequence]' \
        --input-path {input} \
        --output-path {output} 
        '''


rule Extract_V3V4 :
    conda :
        "../../envs/QIIME2-2019.01.yml"
    input :
        config["Output_folder"] + "EzBioCloud_seq.qza"
    output :
        config["Output_folder"] + "EzBioCloudV3V4_seq.qza"
    params :
        primer_forward = config["forward_primer"],
        primer_reverse = config["reverse_primer"],
        length = config["length_max"]
    threads :
        1
    shell :
         '''
        qiime feature-classifier extract-reads \
        --i-sequences {input} \
        --p-f-primer {params[primer_forward]} \
        --p-r-primer {params[primer_reverse]} \
        --p-trunc-len {params[length]} \
        --o-reads {output} 
        '''



rule Export :
   conda :
        "../../envs/QIIME2-2019.01.yml"
   input :
        config["Output_folder"] + "EzBioCloudV3V4_seq.qza"
   output :
        config["Output_folder"] + "dna-sequences.fasta"
   threads :
        1
   shell :
         '''
        qiime tools export \
        --input-path {input} \
        --output-path $(dirname {output}) 
        '''



rule Vsearch :
    conda :
        "../../envs/QIIME2-2019.01.yml"
    input :
        config["Output_folder"] + "dna-sequences.fasta"
    output :
        fasta = config["Output_folder"] + "EzBioCloud_V3V4.fasta",
        uc = config["Output_folder"] + "EzBioCloud_V3V4.uc"
    log :
        config["Output_folder"] + "EzBioCloud_V3V4.log"
    threads :
        1
    shell :
         '''
        vsearch \
        --derep_fulllength {input} \
        --output {output[fasta]} \
        --uc {output[uc]} \
        2> {log}
        '''



rule Taxonomy :
    conda :
        "../../envs/R_DB_processing.yml"
    input :
        tax = config["DBpath_tax"],
        uc = config["Output_folder"] + "EzBioCloud_V3V4.uc"
    output :
        filtrated = config["Output_folder"] + "EzBioCloud1_V3V4_taxonomy.txt",
        all = config["Output_folder"] + "EzBioCloud1_V3V4_all_taxonomy.txt",
        qiime = config["Output_folder"] + "EzBioCloud_V3V4_taxonomy.txt",
        qiime_all = config["Output_folder"] + "EzBioCloud_V3V4_all_taxonomy.txt"
    log :
       config["Output_folder"] + "EzBioCloud_V3V4_taxonomy.log"
    threads :
        1
    script :
        "scripts/DB_EzBioCloud_processing.R"